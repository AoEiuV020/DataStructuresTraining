apply plugin: 'java-library'
apply plugin: 'maven'

group = 'com.github.AoEiuV020'
compileJava.options.encoding = 'UTF-8'
sourceCompatibility = 1.8
targetCompatibility = 1.8
repositories {
    jcenter()
}
dependencies {
    implementation 'com.google.guava:guava:20.0'
    implementation 'org.slf4j:slf4j-api:1.7.25'
    testImplementation 'org.slf4j:slf4j-simple:1.7.25'
    testImplementation 'junit:junit:4.12'
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def linksList = new LinkedList<>();
linksList.add "https://docs.oracle.com/javase/8/docs/api"
linksList.add "https://google.github.io/guava/releases/21.0/api/docs/"
// 这个javadoc.io不行，斜杠/结尾的链接返回302 moved, 可能是因为这个导致 Error fetching URL
// configurations.implementation.dependencies.each {
//     linksList.add "https://www.javadoc.io/doc/$it.group/$it.name/$it.version" as String
// }
tasks.withType(Javadoc) {
    options {
        addStringOption('Xdoclint:none', '-quiet') //ignore all warnings,
        encoding "utf-8"
        docEncoding 'utf-8'
        charSet 'utf-8'
        links linksList.toArray([] as String[])
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// To specify a license in the pom:
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}
